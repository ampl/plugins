
option display_precision 0;
model diet.mod;

param dname symbolic := "amplxl";
param fname_out symbolic := "diet_xlsx/diet_inout_single_2.xlsx";

table Amounts IN (dname) (fname_out):
    [NUTR,FOOD], amt;

table Nutrients IN (dname) (fname_out):
    NUTR <- [NUTR], n_min, n_max;

table Foods (dname) (fname_out):
    [FOOD] IN, cost IN, f_min IN, f_max IN,
    Buy OUT, Buy.rc ~ BuyRC OUT,
    {j in FOOD} Buy[j]/f_max[j] ~ BuyFrac OUT;

read table Foods;
read table Nutrients;
read table Amounts;

display NUTR;
display FOOD;
display amt;

solve;

display {j in FOOD} (Buy[j], Buy.rc[j],  Buy[j]/f_max[j]);

write table Foods;

# read exported table to validate results
reset;
option display_precision 0;

param dname symbolic := "amplxl";
param fname_out symbolic := "diet_xlsx/diet_inout_single_2.xlsx";

set FOOD;
param cost{FOOD};
param f_min{FOOD};
param f_max{FOOD};
param Buy{FOOD};
param BuyFrac{FOOD};
param BuyRC{FOOD};

table Foods IN (dname) (fname_out):
    FOOD <- [FOOD], cost, f_min, f_max, Buy, BuyFrac, BuyRC;

read table Foods;

display FOOD;
display cost;
display f_min;
display f_max;
display Buy;
display BuyFrac;
display BuyRC;
