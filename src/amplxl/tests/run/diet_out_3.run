
option display_precision 0;
model diet.mod;

param dname symbolic := "amplxl";
param fname_in symbolic := "diet_in_3.xlsx";
param fname_out symbolic := "diet_out_3.xlsx";

table Amounts IN (dname) (fname_in):
    [NUTR,FOOD], amt;
table Foods IN (dname) (fname_in):
    FOOD <- [FOOD], cost, f_min, f_max;
table Nutrients IN (dname) (fname_in):
    NUTR <- [NUTR], n_min, n_max;

read table Foods;
read table Nutrients;
read table Amounts;

display NUTR;
display FOOD;
display amt;

solve;

display {j in FOOD} (Buy[j], Buy.rc[j],  Buy[j]/f_max[j]);

table ExportFoods OUT (dname) (fname_out) "Foods":
    [FOOD], cost, f_min, f_max, Buy, Buy.rc ~ BuyRC, {j in FOOD} Buy[j]/f_max[j] ~ BuyFrac;
write table ExportFoods;

# read exported table to validate results
reset;
option display_precision 0;

param dname symbolic := "amplxl";
param fname_out symbolic := "diet_out_3.xlsx";

set FOOD;
param cost{FOOD};
param f_min{FOOD};
param f_max{FOOD};
param Buy{FOOD};
param BuyFrac{FOOD};
param BuyRC{FOOD};

table Foods IN (dname) (fname_out):
    FOOD <- [FOOD], cost, f_min, f_max, Buy, BuyFrac, BuyRC;

read table Foods;

display FOOD;
display cost;
display f_min;
display f_max;
display Buy;
display BuyFrac;
display BuyRC;
